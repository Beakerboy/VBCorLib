@remarks The search assumes the <a href="ArrayList.html">ArrayList</a> is sorted.
<p>The default comparison method requires the values to be of the same
intrinsic Visual Basic datatype. A <b>vbLong</b> will fail to compare against a <b>vbInteger</b>,
for instance. Objects must implement the <a href="IComparable.html">IComparable</a> interface or
an exception will be thrown.</p>
<p>The comparison behaviour can be overridden by supplying a custom <a href="IComparer.html">IComparer</a> compatible object.</P>
<p>If the return value is negative, then the value was not found in the list. To
determine where the value should have been found, negate (Not) the return value.</p>
@exceptions
<table style="width: auto">
    <tr>
        <th>Exception</th><th>Condition</th>
    </tr>
    <tr>
        <td><a href="ArgumentException.html">ArgumentException</a></td>
        <td><i>Comparer</i> is <b>Nothing</b> and neither <i>Value</i> nor the elements in the list support the <a href="IComparable.html">IComparable</a> interface.</td>
    </tr>
    <tr>
        <td><a href="InvalidOperationException.html">InvalidOperationException</a></td>
        <td><i>Comparer</i> is <b>Nothing</b> and <i>Value</i> is not the same type as the elements in the <a href="ArrayList.html">ArrayList</a>.</td>
    </tr>
</table>
@example
The following code demonstrates how to search for a value in an <b>ArrayList</b> using BinarySearch.
<pre>
Private Sub Main()
    Dim list As New ArrayList
    Dim i As Integer
    
    For i = 1 To 5
        list.Add i * 2
    Next i
    
    '' Display the current values in the list
    Console.WriteLine "The ArrayList contains:"
    PrintValues list
    
    '' Find a non-existing value
    FindValue list, 7
    
    '' Find an existing value
    FindValue list, 4
    
    Console.ReadLine
End Sub

Private Sub PrintValues(ByVal en As IEnumerable)
    Dim v As Variant
    
    Console.WriteValue vbTab
    
    For Each v In en
        Console.WriteValue "{0} ", v
    Next v
    Console.WriteLine
End Sub

Private Sub FindValue(ByVal list As ArrayList, ByVal value As Variant)
    Dim Index As Long
    
    Index = list.BinarySearch(value)
    
    If Index < 0 Then
        Console.WriteLine "The value ({0}) was not found in the list. The next largest value was found at index {1}.", value, Not Index
    Else
        Console.WriteLine "the value ({0}) was found at index {1}.", value, Index
    End If
End Sub

'' This code produces the following output.
''
'' The ArrayList contains:
''     2 4 6 8 10
'' The value (7) was not found in the list. the next largest value was found at index 3.
'' The value (4) was found at index 1.
</pre>
