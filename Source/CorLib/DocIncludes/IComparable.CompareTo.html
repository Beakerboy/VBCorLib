@remarks The <b>CompareTo</b> method is implemented by types whose values can be ordered or sorted. It is called automatically by methods of
collection objects, such as <a href="CorArray.Sort.html">CorArray.Sort</a>, to order each member of the array. If a custom class does not 
implement <a href="IComparable.html">IComparable</a>, its members cannot be ordered and the sort operation can throw an <a href="InvalidOperationException.html">InvalidOperationException</a>.
<p>This method is only a definition and must be implemented by a specific class or value type to have effect. The meaning of the comparisons specified in the 
Return Value section ("precedes", "occurs in the same position as", and "follows") depends on the particular implementation.</p>
<p>By definition, any object compares greater than (or follows) <b>Nothing</b>, and two <b>Nothing</b> references compare equal to each other.</p>
<p>The parameter, <i>Value</i>, must be the same type as the class or value type that implements this interface; otherwise, an <a href="ArgumentException.html">ArgumentException</a> is thrown.</p>
@exceptions
<table style="width: auto">
  <tr>
      <th>Exception</th>
      <th>Condition</th>
  </tr>
  <tr>
       <td><a href="ArgumentException.html">ArgumentException</a></td><td><i>Value</i> is not of the same type as this instance.</td>
  </tr>
</table>
@example The following example illustrates the use of <b>CompareTo</b> to compare a <b>Temperature</b> object implementing <a href="IComparable.html">IComparable</a>
with another object. The <b>Temperature</b> object implements <b>CompareTo</b> by simply wrapping a call to the <a href="Comparer.Compare.html">Comparer.Default.Compare</a> method.
<p>There are two modules created for this example. A BAS module containing the <b>Main</b> method
and a Class module implementing the <a href="IComparable.html">IComparable</a> interface.</p>
<p>The following is a <b>Visual Basic Class</b> module named <b>Temperature</b>.</p>
<pre>
Option Explicit
Implements IComparable

Private mTemperatureF As Double

Public Function CompareTo(ByRef Value As Variant) As Long
    Dim Other As Temperature
    
    On Error GoTo Catch
    Set Other = Value
    
    If Other Is Nothing Then
        CompareTo = 1
    Else
        CompareTo = Comparer.Default.Compare(mTemperatureF, Other.Fahrenheit)
    End If
    Exit Function
    
Catch:
    Throw NewArgumentException("Value is not a Temperature")
End Function

Public Property Get Fahrenheit() As Double
    Fahrenheit = mTemperatureF
End Property

Public Property Let Fahrenheit(ByVal Value As Double)
    mTemperatureF = Value
End Property

Public Property Get Celsius() As Double
    Celsius = (mTemperatureF - 32) * (5# / 9#)
End Property

Public Property Let Celsius(ByVal Value As Double)
    mTemperatureF = (Value * 9# / 5#) + 32
End Property


''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''   IComparable
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Private Function IComparable_CompareTo(Value As Variant) As Long
    IComparable_CompareTo = CompareTo(Value)
End Function
</pre>
<p>The following is a <b>Visual Basic BAS</b> module containing <b>Main</b> start-up method.</p>
<pre>
Public Sub Main()
    Dim Temperatures As New ArrayList
    Dim r As New Random
    Dim i As Long
    Dim Degrees As Double
    Dim Temp As Temperature
    
    For i = 1 To 10
        Degrees = r.NextRange(0, 100)
        Set Temp = New Temperature
        Temp.Fahrenheit = Degrees
        Temperatures.Add Temp
    Next
    
    Temperatures.Sort
    
    For Each Temp In Temperatures
        Debug.Print Temp.Fahrenheit
    Next
End Sub

'' This code produces the following output (individual values may vary because they are randomly generated):
''
''    13
''    18
''    31
''    36
''    65
''    65
''    66
''    78
''    92
''    98
</pre>